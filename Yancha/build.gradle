buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.3+'

    }
}

apply plugin: 'android'

def version = readVersion(file("VERSION"))

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode version.toVersionCode()
        versionName version.toVersionName()
    }


    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }

    signingConfigs {
        release {
            storeFile file(project.properties.storeFile)
            storePassword project.properties.storePassword
            keyAlias project.properties.storeAlias
            keyPassword project.properties.keyPassword
        }
    }

    buildTypes {

        // 本番(Playストア)
        release {
            debuggable false
            runProguard false
            signingConfig signingConfigs.release
        }

        // ステージング(Playストア アルファ版)
        alpha.initWith(buildTypes.release)
        alpha {
            debuggable true
            packageNameSuffix ".alpha"
            versionNameSuffix "-alpha"
        }

        // デバッグ(開発環境)
        debug {
            debuggable true
            runProguard false
            packageNameSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    //TODO ぼちぼちつぶしていこう
    lintOptions {
        abortOnError false
    }

}

repositories {
    maven { url 'https://raw.github.com/ichigotake/ColorfulSweets/develop/repository' }
    maven { url 'https://raw.github.com/ichigotake/Android-YanchaSDK/develop/repository' }
}

dependencies {
    compile fileTree(dir: 'src/main/libs', include: '*.jar')
    compile 'com.android.support:appcompat-v7:+'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'com.nhaarman.listviewanimations:library:2.5.2'
    compile 'com.google.guava:guava:14.0-rc1'
    compile 'org.twitter4j:twitter4j-core:3.0.4'
    compile 'net.ichigotake.colorfulsweets:ColorfulSweets:0.0.9'
    compile 'net.ichigotake.yancha:YanchaSDK:0.0.1'
}


android.applicationVariants.all { variant ->
    variant.mergeResources.dependsOn {
        def resourceFile = file("src/main/res/values/_app.xml")
        switch (variant.buildType.name) {
            case "release":
                writeResources(resourceFile, ["yc_app_name": appName])
                break
            case "alpha":
                writeResources(resourceFile, ["yc_app_name": appName + "alpha"])
                break
            case "debug":
                writeResources(resourceFile, ["yc_app_name": appName + " dev"])
                break
        }
    }
}