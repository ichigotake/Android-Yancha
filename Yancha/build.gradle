buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'android'
apply plugin: 'crashlytics'

def version = readVersion(file("VERSION"))

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode version.toVersionCode()
        versionName version.toVersionName()
    }


    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }

    signingConfigs {
        release {
            storeFile file(project.properties.storeFile)
            storePassword project.properties.storePassword
            keyAlias project.properties.storeAlias
            keyPassword project.properties.keyPassword
        }
    }

    buildTypes {

        release {
            debuggable false
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            runProguard false
            packageNameSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    //TODO ぼちぼちつぶしていこう
    lintOptions {
        abortOnError false
    }

}

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
    maven { url 'https://raw.github.com/ichigotake/ColorfulSweets/develop/repository' }
    maven { url 'https://raw.github.com/ichigotake/Android-YanchaSDK/develop/repository' }
    maven { url 'https://raw.github.com/ichigotake/guava-libraries-optional/master/repository' }
}

dependencies {
    compile fileTree(dir: 'src/main/libs', include: '*.jar')
    compile 'com.google.guava:guava-optional:16.0.1'
    compile 'net.ichigotake.colorfulsweets:ics:0.0.4'
    compile 'net.ichigotake.yancha:yancha-sdk:0.0.5'
    compile 'de.greenrobot:eventbus:2.2.0'
    compile 'com.google.android:support-v4:r7' // for 'de.greenrobot:eventbus'
    compile 'com.crashlytics.android:crashlytics:1.+'
}


android.applicationVariants.all { variant ->
    variant.mergeResources.dependsOn {
        def resourceDir = file("src/${variant.buildType.name}/res/values")
        def resourceFile = file("${resourceDir}/_app.xml")
        def resources = [
                "yc_app_name": project.properties.appName,
                "crashlytics_api_key": project.properties.debugCrashlyticsApiKey,
        ]

        resourceDir.mkdirs()
        if (variant.buildType.name != 'release') {
            resources.yc_app_name += variant.buildType.versionNameSuffix
        }

        writeResources(resourceFile, resources)
    }
}
